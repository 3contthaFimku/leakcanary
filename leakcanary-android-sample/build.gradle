plugins {
  id("com.android.application")
  id("org.jetbrains.kotlin.android")
  // Required to run obfuscated instrumentation tests:
  // ./gradlew leakcanary-android-sample:connectedCheck -Pminify
  id("com.slack.keeper") version "0.7.0"
}

keeper {
  variantFilter {
    setIgnore(!project.hasProperty('minify'))
  }
}

dependencies {
  debugImplementation project(':leakcanary-android')
  // Uncomment to use the :leakcanary process
  // debugImplementation project(':leakcanary-android-process')
  releaseImplementation project(':leakcanary-android-release')
  // Optional
  releaseImplementation project(':leakcanary-object-watcher-android')

  implementation deps.kotlin.stdlib
  // Uncomment to use WorkManager
  // implementation deps.androidx.work.runtime

  testImplementation deps.junit
  testImplementation deps.robolectric

  androidTestImplementation project(':leakcanary-android-instrumentation')
  androidTestImplementation deps.androidx.test.espresso
  androidTestImplementation deps.androidx.test.rules
  androidTestImplementation deps.androidx.test.runner
  androidTestImplementation deps.androidx.test.junit
  androidTestImplementation deps.androidx.test.junit_ktx
  androidTestUtil deps.androidx.test.orchestrator
}

android {
  compileSdk versions.compileSdk

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.example.leakcanary"
    minSdk 16
    targetSdk versions.compileSdk

    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // Run ./gradlew leakcanary-android-sample:connectedCheck -Porchestrator
    if (project.hasProperty('orchestrator')) {
      testInstrumentationRunnerArguments clearPackageData: 'true'
      testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
      }
    }
  }

  buildTypes {
    // Build with ./gradlew leakcanary-android-sample:installDebug -Pminify
    if (project.hasProperty('minify')) {
      debug {
        minifyEnabled true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
      }
    } else {
      debug
    }
    release {
      signingConfig signingConfigs.debug
    }
  }

  dexOptions {
    dexInProcess false
  }

  lintOptions {
    disable 'GoogleAppIndexingWarning'
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}
